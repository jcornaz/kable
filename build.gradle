group 'com.github.jcornaz.kable'
version '2.0.0'

buildscript {
    ext.api_version = '2.0'

    ext.kotlin_version = '1.1.4-3'
    ext.dokka_version = '0.9.15'
    ext.gradle_git_version = '0.3.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.ajoberstar:gradle-git-publish:$gradle_git_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.ajoberstar.git-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dokka {
    includes = ['packages.md']
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gitPublish {
    repoUri = 'git@github.com:jcornaz/kable.git'
    branch = 'gh-pages'

    contents {
        from dokka
        into "doc/${api_version}"
    }

    preserve {
        include 'doc/1.1/**'
        include 'doc/1.2/**'
    }
}

check.dependsOn jacocoTestReport
