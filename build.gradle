group 'com.github.jcornaz.kable'
version '2.1-SNAPSHOT'

buildscript {
    ext.api_version = '2.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.15'
        classpath 'org.ajoberstar:gradle-git-publish:0.3.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'kotlin-platform-common'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.ajoberstar.git-publish'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-common'

    testCompile 'org.jetbrains.kotlin:kotlin-test-common'
    testCompile 'org.jetbrains.kotlin:kotlin-test-annotations-common'
}

dokka {
    includes = ['packages.md']
}

task sourcesJar(type: Jar) {
    from sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
// TODO   archives javadocJar
}

gitPublish {
    repoUri = 'git@github.com:jcornaz/kable.git'
    branch = 'gh-pages'

    contents {
        from dokka
        into "doc/${api_version}"
    }

    preserve {
        include 'doc/1.1/**'
        include 'doc/1.2/**'
        include 'doc/2.0/**'
    }
}
